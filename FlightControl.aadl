package FlightControl
public
	----------------
	--- INCLUDES ---
	----------------
	with C_Types;
	with Data_Model;
	with Base_Types; 
	
	------------
	--- DATA --- 
	------------
	
	-- IMU DATA
	data imu_data
		properties
			Data_Model::Data_Representation => Struct;
	end imu_data;
	
	data implementation imu_data.impl
		subcomponents
			acc_x:  data C_Types::uint16_t;
			acc_y:  data C_Types::uint16_t;
			acc_z:  data C_Types::uint16_t;
			gyro_x: data C_Types::uint16_t;
			gyro_y: data C_Types::uint16_t;
			gyro_z: data C_Types::uint16_t;
	end imu_data.impl;

	-- RC IN
	data sbus_frame
		properties
			Data_Model::Base_Type => (classifier (C_Types::uint8_t));
			Data_Model::Data_Representation => Array;
			Data_Model::Dimension => (25);
	end sbus_frame;

	-- ATTITUDE DATA
	data attitude_data
		properties
			Data_Model::Data_Representation => Struct;
	end attitude_data;
	
	data implementation attitude_data.impl
		subcomponents
			roll:  data C_Types::float;
			pitch: data C_Types::float;
			yaw:   data C_Types::float;
	end attitude_data.impl;
	
	-- MOTOR COMMAND
	data motor_command
		properties
			Data_Model::Base_Type => (classifier (C_Types::uint16_t));
	end motor_command;
	
	-- FLIGHT MODE
	data flight_mode_type
		properties
			Data_Model::Base_Type => (classifier (C_Types::uint8_t));
	end flight_mode_type;
	
	-- SYSTEM STATUS
	data system_status
		properties
			Data_Model::Data_Representation => Struct;
	end system_status;
	
	data implementation system_status.impl
		subcomponents
			is_armed: 	 data Base_Types::Boolean;
			battery_v: 	 data C_Types::float;
			flight_mode: data flight_mode_type;
	end system_status.impl;
	
	-- TELEMETRY DATA
	data telemetry_data
		properties
			Data_Model::Data_Representation => Struct;
	end telemetry_data;
	
	data implementation telemetry_data.impl
		subcomponents
			vbat: data C_Types::float;
			rssi: data C_Types::uint16_t;
			roll: data C_Types::float;
			pitch: data C_Types::float;
	end telemetry_data.impl;

	-------------------
	--- BUS TYPES ---
	-------------------
	
	bus spi
	end spi;
	bus implementation spi.impl
	end spi.impl;
	
	bus uart
	end uart;
	bus implementation uart.impl
	end uart.impl;
	
	bus qspi
	end qspi;
	bus implementation qspi.impl
	end qspi.impl;
	
	bus pwm
	end pwm;
	bus implementation pwm.impl
	end pwm.impl;

	---------------
	--- DEVICES ---
	---------------
	
	-- IMU
	device IMU
		features
			get_imu_data: out data port imu_data;
			spi_bus: 	  requires bus access spi; 
	end IMU;
	
	-- RECEIVER
	device Receiver
		features
			rc_data_out: out data port sbus_frame;
			uart_bus: 	 requires bus access uart;
	end Receiver;
	
	-- BATTERY SENSOR
	device Battery_Sensor
		features
			voltage_out: out data port C_Types::float;
	end Battery_Sensor;
	
	-- TELEMETRY TX
	device Telemetry_Transmitter
		features
			telemetry_in: in data port telemetry_data;
			uart_bus: requires bus access uart;
	end Telemetry_Transmitter;
	
	-- ESC
	device ESC
		features
			pwm_in:  in data port motor_command;
			pwm_bus: requires bus access pwm;
	end ESC;
	
	---------------
	--- MEMORY ---
	---------------
	
	-- External_Flash
	memory External_Flash
		features
			qspi_bus: requires bus access qspi; 
	end External_Flash;
	
	memory implementation External_Flash.impl
	end External_Flash.impl;
	

	---------------
	--- THREADS ---
	---------------
	
	-- IMU DRIVER
	thread IMU_Driver
		features
			imu_raw_in: 	  in data port imu_data;
			imu_filtered_out: out data port imu_data;
	end IMU_Driver;
	
	thread implementation IMU_Driver.impl
	end IMU_Driver.impl;
	
	-- RC HANDLER
	thread RC_Handler
		features
			rc_frame_in: 		 in data port sbus_frame;
			target_attitude_out: out data port attitude_data;
			throttle_out: 		 out data port C_Types::float;
			aux_channels_out:	 out data port C_Types::uint16_t;
	end RC_Handler;
	
	thread implementation RC_Handler.impl
	end RC_Handler.impl;
	
	-- BATTERY MONITOR
	thread Battery_Monitor
		features
			adc_voltage_in: in data port C_Types::float;
			vbat_out:		out data port C_Types::float;
	end Battery_Monitor;
	
	thread implementation Battery_Monitor.impl
	end Battery_Monitor.impl;
	
	-- FLIGHT MODE MANAGER
	thread Flight_Mode_Manager
		features
			aux_channels_in: in data port C_Types::uint16_t;
			system_status_in: in data port system_status;
			
			current_mode_out: out data port flight_mode_type;
	end Flight_Mode_Manager;
	
	thread implementation Flight_Mode_Manager.impl
	end Flight_Mode_Manager.impl;
	
	-- SAFETY MONITOR
	thread Safety_Monitor
		features
			throttle_in: 	in data port C_Types::float;
			current_mode_in: in data port flight_mode_type;
			imu_in:			in data port imu_data;
			vbat_in:		in data port C_Types::float;
			
			system_status_out: out data port system_status;
	end Safety_Monitor;
	
	thread implementation Safety_Monitor.impl
	end Safety_Monitor.impl;
	
	-- ATTITUDE ESTIMATOR
	thread Attitude_Estimator
		features
			imu_data_in: 		  in data port imu_data;
			current_attitude_out: out data port attitude_data;
	end Attitude_Estimator;
	
	thread implementation Attitude_Estimator.impl
	end Attitude_Estimator.impl;
	
	-- PID CONTROLLER
	thread PID_Controller
		features
			system_status_in: in data port system_status;
			current_att_in:   in data port attitude_data;
			target_att_in: 	  in data port attitude_data;
			throttle_in: 	  in data port C_Types::float;
			
			pid_roll_out:   out data port C_Types::float;
			pid_pitch_out:  out data port C_Types::float;
			pid_yaw_out:    out data port C_Types::float;
			throttle_mix_out: out data port C_Types::float;
	end PID_Controller;
	
	thread implementation PID_Controller.impl
	end PID_Controller.impl;
	
	-- MIXER
	thread Motor_Mixer
		features
			system_status_in: in data port system_status;
			pid_roll:  in data port C_Types::float;
			pid_pitch: in data port C_Types::float;
			pid_yaw:   in data port C_Types::float;
			throttle:  in data port C_Types::float;
			
			motor1_out: out data port motor_command;
			motor2_out: out data port motor_command;
			motor3_out: out data port motor_command;
			motor4_out: out data port motor_command;
	end Motor_Mixer;
	
	thread implementation Motor_Mixer.impl
	end Motor_Mixer.impl;
	
	-- TELEMETRY MANAGER
	thread Telemetry_Manager
		features
			vbat_in: in data port C_Types::float;
			rssi_in: in data port C_Types::uint16_t;
			attitude_in: in data port attitude_data;
			
			telemetry_out: out data port telemetry_data;
	end Telemetry_Manager;
	
	thread implementation Telemetry_Manager.impl
	end Telemetry_Manager.impl;
	
	-- BLACKBOX LOGGER
	thread Blackbox_Writer
		features
			raw_imu_in:    in data port imu_data;
			rc_in: 		   in data port sbus_frame;
			attitude_in:   in data port attitude_data;
			pid_roll_in:   in data port C_Types::float;
			motors_in:	   in data port motor_command; 
	end Blackbox_Writer;
	
	thread implementation Blackbox_Writer.impl
	end Blackbox_Writer.impl;
	
	
	------------------
	--- SUBSYSTEMS ---
	------------------
	
	-- SUBSYSTEM 1: DATA ACQUISITION
	process Subsys_Data_Acquisition
		features
			hw_imu_in:  in data port imu_data;
			hw_rc_in:   in data port sbus_frame;
			hw_vbat_in: in data port C_Types::float;
			
			imu_data_out: 	out data port imu_data;
			rc_target_out: 	out data port attitude_data;
			rc_throttle_out:out data port C_Types::float;
			rc_aux_out:		out data port C_Types::uint16_t;
			vbat_out:		out data port C_Types::float;
			
			raw_rc_log_out: out data port sbus_frame;
	end Subsys_Data_Acquisition;
	
	process implementation Subsys_Data_Acquisition.impl
		subcomponents
			th_imu:  thread IMU_Driver.impl;
			th_rc:   thread RC_Handler.impl;
			th_batt: thread Battery_Monitor.impl;
		connections
			c_imu_in: port hw_imu_in -> th_imu.imu_raw_in;
			c_imu_out: port th_imu.imu_filtered_out -> imu_data_out;
			
			c_rc_in: port hw_rc_in -> th_rc.rc_frame_in;
			c_rc_att: port th_rc.target_attitude_out -> rc_target_out;
			c_rc_thr: port th_rc.throttle_out -> rc_throttle_out;
			c_rc_aux: port th_rc.aux_channels_out -> rc_aux_out;
			
			c_bat_in: port hw_vbat_in -> th_batt.adc_voltage_in;
			c_bat_out: port th_batt.vbat_out -> vbat_out;
	end Subsys_Data_Acquisition.impl;
	
	-- SUBSYSTEM 2: FLIGHT LOGIC
	process Subsys_Flight_Logic
		features
			imu_in: 		in data port imu_data;
			target_att_in: 	in data port attitude_data;
			throttle_in: 	in data port C_Types::float;
			aux_in:			in data port C_Types::uint16_t;
			vbat_in:		in data port C_Types::float;
			
			pid_roll_out: 	out data port C_Types::float;
			pid_pitch_out: 	out data port C_Types::float;
			pid_yaw_out: 	out data port C_Types::float;
			thr_mixed_out: 	out data port C_Types::float;
			sys_status_out: out data port system_status;
			
			att_log_out: 	out data port attitude_data;
			att_tele_out: 	out data port attitude_data;
			
			mode_out: out data port flight_mode_type;
	end Subsys_Flight_Logic;
	
	process implementation Subsys_Flight_Logic.impl
		subcomponents
			th_est:    thread Attitude_Estimator.impl;
			th_pid:    thread PID_Controller.impl;
			th_safety: thread Safety_Monitor.impl;
			th_fmm:    thread Flight_Mode_Manager.impl;
		connections
			c_aux_fmm: port aux_in -> th_fmm.aux_channels_in;
			c_safety_stat_fmm: port th_safety.system_status_out -> th_fmm.system_status_in; 
			c_fmm_mode: port th_fmm.current_mode_out -> th_safety.current_mode_in; 
			c_fmm_mode_out: port th_fmm.current_mode_out -> mode_out; 

			c_safe_thr: port throttle_in -> th_safety.throttle_in;
			c_safe_imu: port imu_in -> th_safety.imu_in;
			c_safe_bat: port vbat_in -> th_safety.vbat_in;
			c_safe_stat: port th_safety.system_status_out -> sys_status_out;
			c_safe_pid:  port th_safety.system_status_out -> th_pid.system_status_in;
			
			c_est_in: port imu_in -> th_est.imu_data_in;
			c_est_out: port th_est.current_attitude_out -> th_pid.current_att_in;
			c_est_log: port th_est.current_attitude_out -> att_log_out;
			c_est_tele: port th_est.current_attitude_out -> att_tele_out; 
			
			c_pid_tar: port target_att_in -> th_pid.target_att_in;
			c_pid_thr: port throttle_in -> th_pid.throttle_in;
			
			c_pid_r: port th_pid.pid_roll_out -> pid_roll_out;
			c_pid_p: port th_pid.pid_pitch_out -> pid_pitch_out;
			c_pid_y: port th_pid.pid_yaw_out -> pid_yaw_out;
			c_pid_t: port th_pid.throttle_mix_out -> thr_mixed_out;
	end Subsys_Flight_Logic.impl;
	
	-- SUBSYSTEM 3: OUTPUT MANAGEMENT
	process Subsys_Output_Management
		features
			sys_status_in:  in data port system_status;
			pid_roll_in:  	in data port C_Types::float;
			pid_pitch_in: 	in data port C_Types::float;
			pid_yaw_in:   	in data port C_Types::float;
			throttle_in:  	in data port C_Types::float;
			vbat_in:		in data port C_Types::float;
			rc_aux_in:		in data port C_Types::uint16_t; 
			attitude_in: 	in data port attitude_data;
			
			telemetry_out: out data port telemetry_data;
			
			m1_out: out data port motor_command;
			m2_out: out data port motor_command;
			m3_out: out data port motor_command;
			m4_out: out data port motor_command;
	end Subsys_Output_Management;
	
	process implementation Subsys_Output_Management.impl
		subcomponents
			th_mix: thread Motor_Mixer.impl;
			th_tele: thread Telemetry_Manager.impl;
		connections
			c_stat: port sys_status_in -> th_mix.system_status_in;
			c_r: port pid_roll_in -> th_mix.pid_roll;
			c_p: port pid_pitch_in -> th_mix.pid_pitch;
			c_y: port pid_yaw_in -> th_mix.pid_yaw;
			c_t: port throttle_in -> th_mix.throttle;
			
			c_m1: port th_mix.motor1_out -> m1_out;
			c_m2: port th_mix.motor2_out -> m2_out;
			c_m3: port th_mix.motor3_out -> m3_out;
			c_m4: port th_mix.motor4_out -> m4_out;
			
			c_tele_vbat: port vbat_in -> th_tele.vbat_in;
			c_tele_rssi: port rc_aux_in -> th_tele.rssi_in;
			c_tele_att: port attitude_in -> th_tele.attitude_in;
			c_tele_out: port th_tele.telemetry_out -> telemetry_out;
			
	end Subsys_Output_Management.impl;
	
	-- SUBSYSTEM 4: BLACKBOX
	process Subsys_Blackbox
		features
			log_raw_imu: in data port imu_data;
			log_rc:		 in data port sbus_frame;
			log_att:	 in data port attitude_data;
			log_pid_r:	 in data port C_Types::float;
			log_m1:		 in data port motor_command;
	end Subsys_Blackbox;
	
	process implementation Subsys_Blackbox.impl
		subcomponents
			th_logger: thread Blackbox_Writer.impl;
		connections
			c_l1: port log_raw_imu -> th_logger.raw_imu_in;
			c_l2: port log_rc -> th_logger.rc_in;
			c_l3: port log_att -> th_logger.attitude_in;
			c_l4: port log_pid_r -> th_logger.pid_roll_in;
			c_l5: port log_m1 -> th_logger.motors_in;
	end Subsys_Blackbox.impl;

    -- Subsys_Blackbox_System
    system Subsys_Blackbox_System
        features
            log_raw_imu_in: in data port imu_data;
			log_rc_in:		in data port sbus_frame;
			log_att_in:	    in data port attitude_data;
			log_pid_r_in:	in data port C_Types::float;
			log_m1_in:		in data port motor_command;
            
            qspi_bus: requires bus access qspi.impl;
    end Subsys_Blackbox_System;

    system implementation Subsys_Blackbox_System.impl
    	subcomponents
    		external_flash: memory External_Flash;
            proc_logger: process Subsys_Blackbox.impl;
        connections
            c_imu: port log_raw_imu_in -> proc_logger.log_raw_imu;
			c_rc:  port log_rc_in -> proc_logger.log_rc;
			c_att: port log_att_in -> proc_logger.log_att;
			c_pid: port log_pid_r_in -> proc_logger.log_pid_r;
			c_m1:  port log_m1_in -> proc_logger.log_m1;
            
            conn_qspi: bus access qspi_bus <-> external_flash.qspi_bus; 
    end Subsys_Blackbox_System.impl;
	
	
	-----------------
	--- PROCESSOR ---
	-----------------
	
	-- MCU
	processor MCU 
		features
			spi_bus:  requires bus access spi;
			uart_bus: requires bus access uart;
			pwm_bus:  requires bus access pwm;
			qspi_bus: requires bus access qspi;
	end MCU;
	
	--------------------
	--- SYSTEM START ---
	--------------------
	
-- Data_Processing_System
system Data_Processing_System
        features
            hw_imu_in:  in data port imu_data;
            hw_rc_in:   in data port sbus_frame;
            hw_vbat_in: in data port C_Types::float;
            
            telemetry_out: out data port telemetry_data;
            m1_out: out data port motor_command;
            m2_out: out data port motor_command;
            m3_out: out data port motor_command;
            m4_out: out data port motor_command;

            log_raw_imu_out: out data port imu_data;
            log_rc_out:      out data port sbus_frame;
            log_att_out:     out data port attitude_data;
            log_pid_r_out:   out data port C_Types::float;
    end Data_Processing_System;

    system implementation Data_Processing_System.impl
        subcomponents
            proc_input:  process Subsys_Data_Acquisition.impl;
            proc_logic:  process Subsys_Flight_Logic.impl;
            proc_output: process Subsys_Output_Management.impl;
        
        connections
            ------------------------------------
            --- INTERNAL CONNECTIONS ---
            ------------------------------------
            
            sf_imu:  port proc_input.imu_data_out -> proc_logic.imu_in;
            sf_att:  port proc_input.rc_target_out -> proc_logic.target_att_in;
            sf_thr:  port proc_input.rc_throttle_out -> proc_logic.throttle_in;
            sf_aux:  port proc_input.rc_aux_out -> proc_logic.aux_in;
            sf_bat:  port proc_input.vbat_out -> proc_logic.vbat_in;
            
            sf_stat: port proc_logic.sys_status_out -> proc_output.sys_status_in;
            sf_pr:   port proc_logic.pid_roll_out -> proc_output.pid_roll_in;
            sf_pp:   port proc_logic.pid_pitch_out -> proc_output.pid_pitch_in;
            sf_py:   port proc_logic.pid_yaw_out -> proc_output.pid_yaw_in;
            sf_pt:   port proc_logic.thr_mixed_out -> proc_output.throttle_in;
            
            tele_vbat: port proc_input.vbat_out -> proc_output.vbat_in;
            tele_rssi: port proc_input.rc_aux_out -> proc_output.rc_aux_in;
            tele_att:  port proc_logic.att_tele_out -> proc_output.attitude_in;

            ------------------------------------
            --- BOUNDARY CONNECTIONS ---
            ------------------------------------
            
            conn_hw_imu:  port hw_imu_in -> proc_input.hw_imu_in;
            conn_hw_rc:   port hw_rc_in -> proc_input.hw_rc_in;
            conn_hw_vbat: port hw_vbat_in -> proc_input.hw_vbat_in;

            conn_tele_out: port proc_output.telemetry_out -> telemetry_out;
            conn_m1:       port proc_output.m1_out -> m1_out;
            conn_m2:       port proc_output.m2_out -> m2_out;
            conn_m3:       port proc_output.m3_out -> m3_out;
            conn_m4:       port proc_output.m4_out -> m4_out;

            conn_log_imu: port proc_input.imu_data_out -> log_raw_imu_out;
            conn_log_rc:  port proc_input.raw_rc_log_out -> log_rc_out;
            conn_log_att: port proc_logic.att_log_out -> log_att_out;
            conn_log_pid: port proc_logic.pid_roll_out -> log_pid_r_out;

    end Data_Processing_System.impl;

    
    --------------------
    --- TOP LEVEL SYSTEM ---
    --------------------
    
	-- Quadcopter_FC
    system Quadcopter_FC
    end Quadcopter_FC;
    
    system implementation Quadcopter_FC.impl
        subcomponents
            main_mcu: processor MCU;
            ext_flash_mem: memory External_Flash.impl;
            
            bus_spi:  bus spi.impl;
            bus_uart: bus uart.impl;
            bus_pwm:  bus pwm.impl;
            bus_qspi: bus qspi.impl;
            
            sensor_imu:   device IMU;
            sensor_batt:  device Battery_Sensor;
            rx_receiver:  device Receiver;
            telemetry_tx: device Telemetry_Transmitter;
            
            esc_fl: device ESC;
            esc_fr: device ESC;
            esc_rl: device ESC;
            esc_rr: device ESC;
            
            sys_processing: system Data_Processing_System.impl;
            sys_logger:     system Subsys_Blackbox_System.impl; 
            
        connections
            --------------------------
            --- HARDWARE WIRING ---
            --------------------------
            conn_mcu_spi:  bus access bus_spi <-> main_mcu.spi_bus;
            conn_mcu_uart: bus access bus_uart <-> main_mcu.uart_bus;
            conn_mcu_pwm:  bus access bus_pwm <-> main_mcu.pwm_bus;
            conn_mcu_qspi: bus access bus_qspi <-> main_mcu.qspi_bus;
            
            conn_imu_spi:   bus access bus_spi -> sensor_imu.spi_bus;
            conn_rx_uart:   bus access bus_uart -> rx_receiver.uart_bus;
            conn_tele_uart: bus access bus_uart -> telemetry_tx.uart_bus;
            
            conn_fl_qspi_mem: bus access bus_qspi <-> ext_flash_mem.qspi_bus; 
            
            conn_esc1: bus access bus_pwm -> esc_fl.pwm_bus;
            conn_esc2: bus access bus_pwm -> esc_fr.pwm_bus;
            conn_esc3: bus access bus_pwm -> esc_rl.pwm_bus;
            conn_esc4: bus access bus_pwm -> esc_rr.pwm_bus;
            
            ----------------------------
            --- DATA FLOW (SW <-> HW) ---
            ----------------------------
            
            df_imu:  port sensor_imu.get_imu_data -> sys_processing.hw_imu_in;
            df_rx:   port rx_receiver.rc_data_out -> sys_processing.hw_rc_in;
            df_bat:  port sensor_batt.voltage_out -> sys_processing.hw_vbat_in;
            
            df_tele: port sys_processing.telemetry_out -> telemetry_tx.telemetry_in;
            
            df_m1: port sys_processing.m1_out -> esc_fl.pwm_in;
            df_m2: port sys_processing.m2_out -> esc_fr.pwm_in;
            df_m3: port sys_processing.m3_out -> esc_rl.pwm_in;
            df_m4: port sys_processing.m4_out -> esc_rr.pwm_in;
            
            ------------------------------------
            --- LOGGING CONNECTIONS ---
            ------------------------------------
            
            log_1: port sys_processing.log_raw_imu_out -> sys_logger.log_raw_imu_in;
            log_2: port sys_processing.log_rc_out -> sys_logger.log_rc_in;
            log_3: port sys_processing.log_att_out -> sys_logger.log_att_in;
            log_4: port sys_processing.log_pid_r_out -> sys_logger.log_pid_r_in;
            log_5: port sys_processing.m1_out -> sys_logger.log_m1_in;

            conn_logger_qspi: bus access bus_qspi -> sys_logger.qspi_bus;


        properties
            Actual_Processor_Binding => (reference(main_mcu)) applies to sys_processing;
            
            Actual_Processor_Binding => (reference(main_mcu)) applies to sys_logger;
            Actual_Memory_Binding => (reference(ext_flash_mem)) applies to sys_logger;
            
    end Quadcopter_FC.impl;
    
end FlightControl;